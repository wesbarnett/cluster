# Maintainer: James W. Barnett <jbarnet4@tulane.edu>
pkgname=ambertools
pkgver=16
_pkgver=16
pkgrel=1
pkgdesc="Consists of several independently developed packages that work well by
themselves, and with Amber itself. The suite can also be used to carry out
complete molecular dynamics simulations, with either explicit water or
generalized Born solvent models. The sander program is now a part of AmberTools."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.ambermd.org"
depends=('netcdf' 'netcdf-fortran' 'tcsh' 'python' 'libxext' 'python' 'libxt'
'python-scipy' 'python-matplotlib' 'openmpi')
source=( AmberTools${_pkgver}.tar.bz2 
        '0001-DRR-Cpptraj-Switch-to-checking-the-error-fail-bits-o.patch')
md5sums=('ca723e6780f70f46497282c9ea6645a3'
         'd9f68acc69195b42e0bf3d9640d22df0')

# Need to have already downloaded the source

build() {
    CXXFLAGS="-fno-delete-null-pointer-checks $CXXFLAGS"
    CFLAGS="-fno-delete-null-pointer-checks $CLFAGS"
    cd $srcdir/amber${_pkgver}
    ./configure -nofftw3 -noX11 --with-netcdf /usr/ gnu
    test -f $srcdir/amber${_pkgver}/amber.sh && source $srcdir/amber${_pkgver}/amber.sh
    cp ../0001-DRR-Cpptraj-Switch-to-checking-the-error-fail-bits-o.patch AmberTools/src/cpptraj
    cd AmberTools/src/cpptraj
    patch -p1 < 0001-DRR-Cpptraj-Switch-to-checking-the-error-fail-bits-o.patch
    cd -
    make install
}

package() {

    test -f $srcdir/amber${_pkgver}/amber.sh && source $srcdir/amber${_pkgver}/amber.sh
    cd ${srcdir}/amber${_pkgver}

    install -d ${pkgdir}/etc/
    install -d ${pkgdir}/etc/profile.d
    install -d ${pkgdir}/opt/amber${_pkgver}

    cd ${srcdir}/amber${_pkgver}
    directories=$(find -type d | grep -v "src" | grep -v "test")

    for dir in $directories; do
        install -d ${pkgdir}/opt/amber${_pkgver}/$dir
    done

    files=$(find -type f | grep -v "src" | grep -v "test")
    for file in $files; do
        install $file ${pkgdir}/opt/amber${_pkgver}/$file
    done

    sed -i 's/export AMBERHOME.*/export AMBERHOME=\/opt\/amber'${_pkgver}'/' ${srcdir}/amber${_pkgver}/amber.sh
    install -m 755 ${srcdir}/amber${_pkgver}/amber.sh ${pkgdir}/etc/profile.d/
    sed -i 's/setenv AMBERHOME.*/setenv AMBERHOME \/opt\/amber'${_pkgver}'/' ${srcdir}/amber${_pkgver}/amber.csh
    install -m 755 ${srcdir}/amber${_pkgver}/amber.csh ${pkgdir}/etc/profile.d/


}

check() {
    cd ${srcdir}/amber${_pkgver}
    make test
}


