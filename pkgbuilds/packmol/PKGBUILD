# James W. Barnett <jbarnet4@tulane.edu>
pkgname=packmol
pkgver=15.133
pkgrel=1
pkgdesc="Creates an initial point for molecular dynamics simulations by packing molecules in defined regions of space."
depends=('gcc-libs' 'tcl' 'bash')
makedepends=('gcc-fortran')
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.ime.unicamp.br/~martinez/packmol/"
source=(http://leandro.iqm.unicamp.br/packmol/versionhistory/packmol-${pkgver}.tar.gz)
sha1sums=('ae8a045a3ef43b8dbaafbd3cff0020ccbe232303')

build() {
  cd ${srcdir}/packmol
  sed -i 's/maxatom     =    500000/maxatom     =   2000000/g' sizes.f90
  sed -i 's/maxtype     =        50/maxtype     =       150/g' sizes.f90
  ./configure
  make
}

package() {
  cd ${srcdir}/packmol
  install -D -m755 packmol ${pkgdir}/usr/bin/packmol
}



